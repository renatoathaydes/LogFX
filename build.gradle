import groovy.text.SimpleTemplateEngine

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
    }
}

group 'com.athaydes.logfx'
version '0.9.1'

apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "com.athaydes.logfx.LogFX"

repositories {
    mavenLocal()
    jcenter()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

final generatedJavaSources = "$buildDir/java-generated-sources"

sourceSets.main.java.srcDir generatedJavaSources

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'

    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.4'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4', {
        transitive = false
    }
    testCompile group: 'com.athaydes', name: 'spock-reports', version: '1.3.2', {
        transitive = false
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'cglib', name: 'cglib-nodep', version: '3.2.5'
    testCompile group: 'com.google.code.tempus-fugit', name: 'tempus-fugit', version: '1.1'
}

test {
    systemProperty 'logfx.log.target', 'sysout'
    systemProperty 'logfx.log.level', 'info'
    systemProperty 'com.athaydes.spockframework.report.showCodeBlocks', 'true'
}

task generateJavaSources(
        description: 'Generates Java sources based on the java-templates directory' ) {
    def properties = [ logfxVersion: version ]
    def templates = fileTree( dir: 'src/main/java-templates' )
    def destination = file( generatedJavaSources )

    doLast {
        templates.visit { item ->
            def file = item.file
            if ( file.isFile() ) {
                def transformedTemplate = new SimpleTemplateEngine().createTemplate( file ).make( properties )
                new File( destination, item.relativePath.toString() ).withWriter {
                    transformedTemplate.writeTo it
                }
            } else {
                new File( destination, item.relativePath.toString() ).mkdirs()
            }
        }
    }
}

compileJava.dependsOn generateJavaSources

task fatJar( type: Jar ) {
    manifest {
        attributes 'Implementation-Title': 'LogFX',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Renato Athaydes',
                'Main-Class': mainClassName
    }

    classifier "all"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree( it ) } }
    with jar
}

task sourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar( type: Jar ) {
    classifier "javadoc"
    from javadoc
}

/* Publishing config */

artifacts {
    archives javadocJar, sourcesJar, fatJar
}

// add all the info required by Maven Central to the pom
configure( install.repositories.mavenInstaller ) {
    pom.project {
        inceptionYear '2017'
        name project.name
        packaging 'jar'
        description project.description

        url 'https://github.com/renatoathaydes/LogFX'

        scm {
            connection 'git@github.com:renatoathaydes/LogFX.git'
            developerConnection 'git@github.com:renatoathaydes/LogFX.git'
            url 'https://github.com/renatoathaydes/LogFX'
        }

        licenses {
            license {
                name 'GNU General Public License (version 3.0)'
                url 'https://www.gnu.org/licenses/gpl-3.0.txt'
            }
        }

        developers {
            developer {
                id 'renatoathaydes'
                name 'Renato Athaydes'
                email 'renato@athaydes.com'
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.hasProperty( "bintrayUserName" ) ? bintrayUserName : null
    key = project.hasProperty( "bintrayApiKey" ) ? bintrayApiKey : null
    configurations = [ 'archives' ]
    publish = true
    pkg {
        repo = 'maven'
        name = 'logfx'
        licenses = [ 'GPL-3.0' ]
        desc = project.description
        vcsUrl = 'https://github.com/renatoathaydes/LogFX.git'
        websiteUrl = 'https://github.com/renatoathaydes/LogFX'
        issueTrackerUrl = 'https://github.com/renatoathaydes/LogFX/issues'
        labels = [ 'log', 'javafx', 'log-viewer' ]
        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty( "ossrhUsername" ) ? ossrhUsername : null
                password = project.hasProperty( "ossrhPassword" ) ? ossrhPassword : null
                close = '1' // '0' to NOT close
            }
        }
    }

}

bintrayUpload.dependsOn build, sourcesJar, fatJar
